title Chores!
author Seth, Raferty 
homepage www.puzzlescript.net
again_interval .08

========
OBJECTS
========

Background
lightblue

Target
black white blue transparent gray
03333
11143
11404
11404
11143

Wall
lightgray red
11101
00000
10111
00000
11101

Background2
yellow lightblue
01110
10001
00000
10001
01110

Box
brown

Player
black white blue transparent orange
30003
34443
11111
32223
32323

Crate
transparent #95bd37 #22b14c
00000
11211
01110
01110
01110

climbable
transparent

move_right
transparent

move_left
transparent

move_down
transparent

fall
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
$ = Box
@ = Crate and Target
O = Target
& = Background2

Object = player or wall or crate or Target or Box

fallObstruction = crate or wall or player or box

faller = player or crate or Target or box

movable = crate or Box

move_marker = move_right or move_left or move_down

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Background2
Target
Player, Wall, Crate, Box

move_marker
fall
move_down
move_right
move_left

climbable

======
RULES
======

[ > Player | Crate ] -> [ > Player | > Crate]
[ > Player | Box ] -> [ > Player | > Box]

[ fall faller ] -> [ down faller move_down]
[ fall ] -> []

[ climbable ] -> []
down [ no Wall | wall no climbable] -> [ | wall climbable]
down [ no fallObstruction | movable no climbable | fallObstruction] -> [ | movable climbable | fallObstruction]

[ move_right Player ] -> [ right Player ]
[ move_left Player ] -> [ left Player ]
[ move_marker ] -> []

right [ right Player | climbable ] -> [ up Player up move_right | climbable ] again
left [ left Player | climbable ] -> [ up Player up move_left | climbable  ] again

late down [ Crate no move_marker | no fallObstruction ] -> [ fall Crate | ] again
late down [ Box no move_marker | no fallObstruction ] -> [ fall Box | ] again
late down [ Player no move_marker | no fallObstruction ] -> [ fall Player | ] again

==============
WINCONDITIONS
==============

all Target on Crate

=======
LEVELS
=======


#########
#......&#
#.......#
#.P*....#
#.###...#
#O.....##
#########

###############
#......p.....&#
#..*...$......#
#..##..##.*...#
#...$.....$...#
#...#######...#
#.............#
#.............#
#.............#
#.............#
######o$o######
######$$$######
###############

###########
#.........#
#...$.....#
#...$.....#
#.*.$.....#
#.##$..$..#
#...$..$..#
#...$..$..#
#o..$..$.p#
###########
